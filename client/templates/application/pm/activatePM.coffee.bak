Template.activatePMPage.onCreated ->
  self = this
  self.autorun ->
    self.subscribe 'singlePM', Session.get('currentDoc')._id.toString()
    self.subscribe 'assetsPM', Session.get('currentDoc')._id.toString()
  this.activateAll = new ReactiveVar false
  this.setPM = new ReactiveVar {
    activate: false
  }

Template.activatePMPage.onRendered ->
  $(".dropdown-button").dropdown()
  Session.set 'temp', true

Template.activatePMPage.onRendered ->
  $('.tooltipped').tooltip {delay: 50}

Template.activatePMPage.onDestroyed ->
  $('.tooltipped').tooltip 'remove'

Template.activatePMPage.helpers
  customTemplate: -> Customisations.activatePM
  activateAll: -> Template.instance().activateAll.get()
  viewDoc: ->
    Collections.PM.Current = PM.findOne ({ _id: Session.get('currentDoc')._id.toString() })
    Session.set 'currentDoc', Collections.PM.Current
    Collections.PM.Current
  setPM: -> Template.instance().setPM.get()
  timeBasedPM: -> Collections.PM.Current.pmIntervalType!=1
  workorderPMList: -> Session.get('currentDoc').workorderPM
  settings: ->
    temp = {
      rowsPerPage: 10
      showFilter: true
      fields:  [
        { key: 'asset_ID', label: ' Asset System ID' }
        { key: 'assetName', label: ' Asset' }
      ]
    }
    if Collections.PM.Current.pmIntervalType == 1 # Meter interval type
      temp.fields.push { key: 'meterInterval', label: ' Meter Interval' }
    else
      temp.fields.push { key: 'cronJob', label: ' Text/Cron Expression', sortable: false } # Time interval type
      temp.fields.push { key: 'pmExpression', label: ' Scheduling Expression', sortable: false }

    temp.fields.push { label: 'Activate', tmpl: Template.rtBoolean, sortable: false }
    temp.fields.push { label: 'Change', tmpl: Template.rtEdit, sortable: false }
    temp

Template.activatePMPage.events
  'click #swtActivatePM': (event, template) ->
    template.activateAll.set(!$('#swtActivatePM').prop('checked'))
  'click .frmEdit .btnEdit': (event, template) ->
    temp = Collections.PM.Current.workorderPM
    tempObj = {}
    MaterializeModal.form
      title: "Edit Activation Data"
      bodyTemplate: "activatePMForm"
      callback: (error, response) ->
        if response?.submit
          #Iterate over form results & display.
          for key, value of response.form
            if key == 'activate-pm'
              tempObj.active = value
            else if key == 'cron-expression'
              if value # Add cron Job if it exists
                tempObj.cronJob = value
            else if key == 'pm-expression'
              if value # Add expression if it exists
                tempObj.pmExpression = value
          # Update workorderPM
          for a in [0...temp.length]
            temp[a].active = tempObj.active
            if tempObj.cronJob # Add cronJob if it exists
              temp[a].cronJob = tempObj.cronJob
            if tempObj.pmExpression # Add Expression if it exists
              temp[a].pmExpression = tempObj.pmExpression
          template.setPM.set(tempObj)
          Collections.PM.Current.workorderPM = temp
          Session.set 'currentDoc', Collections.PM.Current
        else
          toast('error', 'Cancelled')
  'click .rtEdit .btnEdit': (event) ->
    if (Collections.PM.Current.pmIntervalType==1) && !(Locations.findOne('_id': this.asset_ID).meters)
      toast 'error', 'Asset has no meters'
      return
    else
      Collections.Locations.CurrentID = this.asset_ID
      temp = Collections.PM.Current.workorderPM
      for a in [0...temp.length]
        if temp[a].asset_ID == this.asset_ID
          tempIndex = a
      MaterializeModal.form
        title: "Edit Activation Data"
        bodyTemplate: "activatePMForm"
        callback: (error, response) ->
          if response?.submit
            #Iterate over form results & display.
            for key, value of response.form
              if key == 'activate-pm'
                temp[tempIndex].active = value
              else if key == 'cron-expression'
                if value # Add cron Job if it exists
                  temp[tempIndex].cronJob = value
              else if key == 'pm-expression'
                if value # Add expression if it exists
                  temp[tempIndex].pmExpression = value
              else if key == 'meter-interval'
                if value # Add meter interval if it exists
                  temp[tempIndex].meterInterval = value
              else if key == 'meter-id'
                if value # Add meter id if it exists
                  temp[tempIndex].meterID = value
            Collections.PM.Current.workorderPM = temp
            Session.set 'currentDoc', Collections.PM.Current
            console.log 'currentDoc: ' + JSON.stringify Session.get('currentDoc').workorderPM
          else
            toast('error', 'Cancelled')
  'submit form': (event, template) ->
    event.preventDefault()
    temp = {$set: {workorderPM: Collections.PM.Current.workorderPM}}
    Meteor.call 'updatePM', temp, Collections.PM.Current._id, (error, result) ->
      if error
        toast 'error', error
      else
        toast 'success', result
      return

Template.activatePMForm.helpers
  timeBasedPM: -> Collections.PM.Current.pmIntervalType==0
  meterList: -> Locations.findOne('_id': Collections.Locations.CurrentID).meters
  aSettings: ->
    temp = {
      rowsPerPage: 10
      showFilter: false
      showNavigation: 'auto'
      fields:  [
        { key: 'id', label: ' Meter ID' }
        { key: 'text', label: ' Meter Name' }
        { key: 'units', label: ' Meter Units', sortable: false }
        { label: 'Activate', tmpl: Template.rtAdd, sortable: false }
      ]
    }
